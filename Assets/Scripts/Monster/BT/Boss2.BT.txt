tree("Root")
    Sequence
        tree("Idle")
        tree("Phase0")
        tree("PhaseChange")
        tree("Phase1")

// 플레이어가 보스방에 들어가기 전 보스 대기 상태
tree("Idle")
  not
    repeat
      not
        isEnemyCloserThan(999.9)  // 플레이어가 인식 범위에 들어오면 Idle 종료

// 1페이즈
tree("Phase0") 
  not                          // Phase 종료 조건 달성했을 때 Fail 대신 Succeed 반환하도록 not 게이트 사용
    repeat 
      sequence
        isHpOverPhaseLimit(0)           // HP 체크하고 만약 HP가 기준 아래(=fail)일 경우 Phase2로 이행
        mute                            // 패턴 하나의 실패로 발생한 fail 때문에 페이즈 바뀌는 거 방지   
            fallback                    // 여기서부터 실제 패턴 등장
                sequence
                    not isEnemyAtFront()        // 적이 등 뒤에 있다면, 뒤돌아보기 패턴 수행
                    tree("DiveByLookBack")
                tree("DiveByCooldown")
                sequence
                    isEnemyCloserThan(5.0)        // 적이 기준보다 가깝다면,
                    fallback                    // 근접용 2패턴을 돌아가면서 수행
                        sequence
                            TikTok()
                            tree("MeleeAttack")     // 근접 공격
                        tree("Shockwave")           // 파도풀 패턴 
                Random()                        // 적이 기준보다 멀다면 원거리용 2패턴 중 랜덤 수행
                    tree("WaterBomb")               // 패턴: 물덩어리 발사
                    tree("WaterLaser")              // 패턴: 물대포

// 페이즈 전환 시
tree("PhaseChange")
    RoarAttack()

// 2페이즈
tree("Phase1")
  repeat                                // 마지막 Phase에서는 HP 체크할 필요 없음.
    mute                                // 패턴 하나의 실패로 발생한 fail 때문에 페이즈 바뀌는 거 방지   
        fallback
            sequence
                not isEnemyAtFront()        // 적이 등 뒤에 있다면, 뒤돌아보기 패턴 수행
                tree("DiveByLookBack")
            tree("Breaching")               // 필살기: 브리칭. 쿨타임 중이라면 알아서 fail -> 다른 패턴 수행
            sequence
                isEnemyCloserThan(5.0)        // 적이 기준보다 가깝다면,
                fallback                    // 근접용 2패턴을 돌아가면서 수행
                    sequence
                        TikTok()
                        tree("MeleeAttack")     // 근접 공격
                    tree("Shockwave")           // 파도풀 패턴 
            Random()                        // 적이 기준보다 멀다면 원거리용 2패턴 중 랜덤 수행
                tree("WaterBomb")               // 패턴: 물덩어리 발사
                tree("WaterLaser")              // 패턴: 물대포


// 여기서부터는 각각의 공격패턴만 따로 구현한 서브트리들
tree("DiveByCooldown")
    sequence
        CheckCooldown("Dive", 12.0)
        tree("Dive")

tree("DiveByLookBack")
    sequence
        ResetCooldown("Dive", 0.0)
        tree("Dive")
        
tree("Dive")
    parallel
        Dive()
        sequence
            Wait(3.1)
            WaterSplash()

tree("MeleeAttack")
    MeleeAttack()

tree("Shockwave")              
    ShockwaveAttack()

tree("WaterBomb")
    SimpleProjectileAttack()

tree("WaterLaser")
    LaserAttack()

tree("Breaching")
    sequence
        CheckCooldown("Breaching", 20.0)
        Breaching()

